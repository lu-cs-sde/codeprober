plugins {
  // id 'groovy'
  id 'maven-publish'
  id 'signing'
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "0.18.0"
}

group = 'org.codeprober'
version = '0.0.4'

// Ensure the plugin runs with Java 8.
sourceCompatibility = targetCompatibility = '1.8'

repositories {
  mavenCentral()
}

configurations {
  // extraLibs is used to pack the json files into the generated jar file
  extraLibs
}

dependencies {
  implementation gradleApi()
  implementation 'org.json:json:20240303'
  extraLibs group: 'org.json', name: 'json', version: '20240303'
}

test {
  testLogging.exceptionFormat = "full"
}

java {
  withJavadocJar()
  withSourcesJar()
}
jar {
      duplicatesStrategy = DuplicatesStrategy.EXCLUDE
      from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'codeprobergradle'
      from components.java

      versionMapping {
        usage('java-api') {
          fromResolutionOf('runtimeClasspath')
        }
        usage('java-runtime') {
          fromResolutionResult()
        }
      }

      pom {
        name = 'CodeproberGradle'
        description = 'Plugin for configuring and running CodeProber'
        url = 'https://github.com/lu-cs-sde/codeprober/tree/master/gradle-plugin'
        licenses {
          license {
            name = 'Modified BSD License'
            url = 'http://opensource.org/licenses/BSD-3-Clause'
            distribution = 'repo'
          }
        }
        developers {
          developer {
            name = 'Anton Risberg Alak√ºla'
            email = 'anton.risberg_alakula@cs.lth.se'
          }
        }
        scm {
          connection = 'scm:git:https://github.com/lu-cs-sde/codeprober.git'
          url = 'https://github.com/lu-cs-sde/codeprober.git'
        }
      }
    }
  }
  if (project.hasProperty('ossrhUsername')) repositories {
    maven {
      url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username = ossrhUsername
        password = ossrhPassword
      }

      // Local repository = 'localPluginRepo'
      // Snapshot repoitory = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }
  if (project.hasProperty('reposiliteUsername')) repositories {
    maven {
      url = 'https://reposilite.nas.kevlanche.com/releases'
      credentials {
        username = reposiliteUsername
        password = reposilitePassword
      }

      // Local repository = 'localPluginRepo'
      // Snapshot repoitory = 'https://oss.sonatype.org/content/repositories/snapshots'
    }
  }
}
signing {
  // Skip signing if there is no key configured:
  required { project.hasProperty('signing.keyId') }
  sign publishing.publications.mavenJava
}

pluginBundle {
  website = 'https://github.com/lu-cs-sde/codeprober/tree/master/gradle-plugin'
  vcsUrl = 'https://github.com/lu-cs-sde/codeprober/'
  description = 'Plugin for configuring and running CodeProber'
  tags = [ 'codeprober', 'jastadd' ]
}

gradlePlugin {
  plugins {
    codeproberPlugin {
      id = 'org.codeprober'
      displayName = 'CodeProber Gradle plugin'
      description = 'Plugin for downloading and starting CodeProber with Gradle.'
      implementationClass = 'org.codeprober.CodeProberPlugin'
    }
  }
}

javadoc {
  if(JavaVersion.current().isJava9Compatible()) {
    options.addBooleanOption('html5', true)
  }
}

// Fix task dependency issue that appeared when upgrading from gradle 7.2 to 8.14.3
// There are probably cleaner solutions that involves rewriting the big publishing
// block above, maybe do that in future updates.
afterEvaluate {
  tasks.named("generateMetadataFileForPluginMavenPublication") {
    dependsOn "publishPluginJar", "publishPluginJavaDocsJar"
  }
  tasks.named("publishMavenJavaPublicationToMavenLocal") {
    dependsOn "publishPluginJar", "publishPluginJavaDocsJar"
  }
  tasks.named("generateMetadataFileForMavenJavaPublication") {
    dependsOn "publishPluginJar", "publishPluginJavaDocsJar"
  }
}
